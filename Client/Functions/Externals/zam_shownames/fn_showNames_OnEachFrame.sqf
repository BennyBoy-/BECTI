/*
	Author(s):
		Phoenix of Zulu-Alpha
		LH

	Purpose:
		Draw each name tag in ZAM_showNames_var_drawJobs

	Params
		None - Uses ZAM_showNames_var_drawJobs generated by ZAM_showNames_fnc_pressLoop

	Return
		Nothing

	Reference
		[
			_rank_path,
			[_colour_r, _colour_g, _colour_b, _alpha],
			_x modelToWorld [0,0,_height],
			_icon_size,
			_icon_size,
			0,
			_name_tag,
			0,
			_text_size,
			_font
		]

			texture: String
			color: Color
			pos: Position
			width: Number
			height: Number
			angle: Number
			text: String - (optional)
			shadow: Number - (optional)
			textSize: Number - (optional)
			font: String - (optional)

*/

// Draw each item in job list
{

		private ["_obj", "_top", "_bottom", "_diff", "_adjust", "_height", "_pos"];

		// The object
		_obj = (_x select 2) select 0;

		//// Calculate the height
		// Top height of bos
		_top = ((boundingBox _obj) select 1) select 2;

		// Bottom height of box
		_bottom = ((boundingBox _obj) select 0) select 2;

		// Absolute value of the difference between the top and bottom
		_diff = abs (_top - _bottom);

		// We have the top height in model coordinates. Now we need to factor in an adjustment from the pressLoop
		_adjust = (_x select 2) select 1;
		_height = _diff + _adjust;

		//// Final position
		// Now we need to convert the model coordinates (as in the height above) into world coordinates
		_pos = _obj modelToWorldVisual [0, 0, _height];


		//// Do the primary draw
		// [texture, color, [object, height], width, height, angle, text, shadow, textSize, font]
		// [object, height] is provided instead of coordinates in order to calculate position in onEachFrame instead
		drawIcon3D [
			_x select 0,
			_x select 1,
			_pos,
			_x select 3,
			_x select 4,
			_x select 5,
			_x select 6,
			2,
			_x select 8,
			_x select 9
		];


} count ZAM_showNames_var_drawJobs;